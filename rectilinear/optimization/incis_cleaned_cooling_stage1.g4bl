# POST 6D - RECTILINEAR LATTICE - STAGE 01 - Version 2.2.1

# QGSP is the "default" physics use-case for HEP
physics QGSP_BERT_EMX 

param pi=3.1415926

# CELL PROPERTIES

param ncells=30 # Number of coil-RF cavity-absorber setup we have
param cell_length=2200 #mm

# COIL AND SOLENOID PROPERTIES

param coilname=coil

param coil_length=596.9838479171975
param coil_radius=450
param coil_width=225

param solpos=0.21428071133122087
param bendpos=0.1
param current=32.14509719624864
param angle=1.5

# RF CAVITY

param pres=35 # PRESSURE

param rf_length=220
param -unset rf_fre=0.352 #mHz
param -unset rf_grad=22.508192486472524
param -unset rf_ph=32.84244060974717

param -unset rf_window_length=0.1
param wallthick=$rf_window_length

material H2 Z=1 A=1.01 density=0.000090*$pres pressure=$pres
param cavity_gas=Vacuum

# DIPOLE PROPERTIES

param -unset dipole=0.058 # B_0
param dipole_length=100

# PIPE AND FRINGE FIELDS
param pipe_radius=230
param pipe_diameter=2*$pipe_radius
param fringe_length=4*$pipe_radius
param pipe_length=($cell_length-6*($rf_length+2*$wallthick))/2

# ABSORBER WEDGE 
param wedge_window_length=0.3 #0.1
param half_wedge_length=370
param wedge_apex=110

# CLOSED ORBIT?

param x_last=2.600925646
param y_last=-5.907135278
param x_off=-6.321091140656247
param y_off=-11.071212987293226


######################## PARTICLES AND DISTRIBUTION #######################

param -unset first=1
param -unset last=2000 # Number of particles in your simulation

param ref_momentum=204 # reference momentum

reference particle=mu+ referenceMomentum=$ref_momentum beamT=0 beamX=$x_off beamY=$y_off noEfield=1 noBfield=1 noEloss=1
beam ascii file=beam_stage1.beam firstEvent=$first lastEvent=$last beamX=$x_off-$x_last beamY=$y_off-$y_last


trackcuts killSecondaries=1
trace nTrace=1

########################## DEFINE STRUCTURES ################################

genericbend solbend fieldWidth=1000 fieldHeight=1000 fieldLength=$dipole_length \
               ironWidth=-1 ironHeight=-1 ironLength=-1 \
               openAperture=1 \
               By=$dipole kill=1 fringe=0 ironColor=1,1,1


fieldexpr fringe_left length=$fringe_length width=2*$pipe_radius height=2*$pipe_radius By=$dipole*((1+exp(5*-z/$pipe_diameter)*cos(5*y/$pipe_diameter))/(1+2*exp(5*-z/$pipe_diameter)*cos(5*y/$pipe_diameter)+exp(2*5*-z/$pipe_diameter))) Bz=-$dipole*exp(5*-z/$pipe_diameter)*sin(5*y/$pipe_diameter)/(1+2*exp(5*-z/$pipe_diameter)*cos(5*y/$pipe_diameter)+exp(2*5*-z/$pipe_diameter)) nX=2 nY=100 nZ=150 
fieldexpr fringe_right length=$fringe_length width=2*$pipe_radius height=2*$pipe_radius By=$dipole*((1+exp(5*z/$pipe_diameter)*cos(5*y/$pipe_diameter))/(1+2*exp(5*z/$pipe_diameter)*cos(5*y/$pipe_diameter)+exp(2*5*z/$pipe_diameter))) Bz=-$dipole*exp(5*z/$pipe_diameter)*sin(5*y/$pipe_diameter)/(1+2*exp(5*z/$pipe_diameter)*cos(5*y/$pipe_diameter)+exp(2*5*z/$pipe_diameter)) nX=2 nY=100 nZ=150 

coil $coilname innerRadius=$coil_radius outerRadius=$coil_radius+$coil_width length=$coil_length material=Cu maxZ=$cell_length maxR=$coil_radius+4*$coil_width #tolerance=0.02
solenoid sol1 coilName=$coilname current=$current kill=1 
solenoid sol2 coilName=$coilname current=-$current kill=1 

tubs pipe innerRadius=$pipe_radius outerRadius=$pipe_radius+1 length=$pipe_length kill=1

pillbox cavity innerLength=$rf_length irisRadius=$pipe_radius frequency=$rf_fre maxGradient=$rf_grad phaseAcc=$rf_ph winMat=Be cavityMaterial=$cavity_gas win1Thick=$rf_window_length win2Thick=$rf_window_length wallThick=$wallthick collarThick=0 kill=1

box wedge_window height=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) length=$wedge_window_length width=2*$half_wedge_length/2/sin($wedge_apex/2*$pi/180) material=Be
trap all_wedge  height=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) upperWidth=0.001 lowerWidth=2*$half_wedge_length length=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) material=LH2


########################## PLACING ELEMENTS ##########################

do i -2 $ncells # started from -2
place sol1 z=$i*$cell_length+$solpos*$cell_length rename=sol_a_$i
place sol2 z=$i*$cell_length+$cell_length-$solpos*$cell_length rename=sol_b_$i
enddo

do i -2 $ncells # started from -2
place solbend z=$i*$cell_length+$bendpos*$cell_length rename=solbend_a_$i
place solbend z=$i*$cell_length+$cell_length-$bendpos*$cell_length rename=solbend_b_$i
enddo

do i -2 $ncells # started from -2
place fringe_left z=$i*$cell_length+$bendpos*$cell_length-$dipole_length/2-$fringe_length/2
place fringe_right z=$i*$cell_length+$bendpos*$cell_length+$dipole_length/2+$fringe_length/2+0.01
place fringe_left z=$i*$cell_length+$cell_length-$bendpos*$cell_length-$dipole_length/2-$fringe_length/2
place fringe_right z=$i*$cell_length+$cell_length-$bendpos*$cell_length+$dipole_length/2+$fringe_length/2+0.01
enddo

do i 0 $ncells
place pipe z=$i*$cell_length+$pipe_length/2
place pipe z=$i*$cell_length+$cell_length-$pipe_length/2
enddo


do i 0 $ncells
place cavity z=$i*$cell_length+$cell_length/2-(2*$rf_length+2*2*$wallthick+$wallthick+$rf_length/2) rename=rf_a_$i
place cavity z=$i*$cell_length+$cell_length/2-($rf_length+2*$wallthick+$wallthick+$rf_length/2) rename=rf_b_$i
place cavity z=$i*$cell_length+$cell_length/2-($wallthick+$rf_length/2) rename=rf_c_$i
place cavity z=$i*$cell_length+$cell_length/2+($wallthick+$rf_length/2) rename=rf_d_$i
place cavity z=$i*$cell_length+$cell_length/2+($rf_length+2*$wallthick+$wallthick+$rf_length/2) rename=rf_e_$i
place cavity z=$i*$cell_length+$cell_length/2+(2*$rf_length+2*2*$wallthick+$wallthick+$rf_length/2) rename=rf_f_$i
enddo

do i 0 $ncells
place all_wedge z=$i*$cell_length  rotation=X90,Y90 x=$x_off rename=wedge_$i
enddo

do i 0 $ncells
place wedge_window z=$i*$cell_length-$half_wedge_length/2-$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off rotation=Y-$wedge_apex/2 color=1,0,1
place wedge_window z=$i*$cell_length+$half_wedge_length/2+$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off rotation=Y$wedge_apex/2 color=1,0,1
enddo

########################################################################################################

zntuple output zloop=0:$ncells*$cell_length:$cell_length format=for009 require=PDGid==-13 file=for009 referenceParticle=1